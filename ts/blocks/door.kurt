[def {
  Door: {
    make: (closed-img open-img | Entity:make [list
      [Located:make]
      [Rendered:make closed-img]
      [{height:2} Solid:make]
      {door: {
        ^: -impl
        open-img closed-img
        locked: true
        open: false
        code: 123
      }}
    ])

    -impl: {
      perform: (ent action | cond
        (and [= action:action :use] [= @:code [?= action :object :key :code]]) (do
          [-> @ {locked: [! @:locked]}]
          [@:-update ent]
        )
        (and [= action:action :open] [! @:locked]) (do
          [-> @ {open: [! @:open]}]
          [@:-update ent]
        )
      )

      -update: (ent | do
        [-> ent:rendered {image: [if @:open @:open-img @:closed-img]}]
        [if [? ent :solid] (
          -> ent:solid {solid: [! @:open]}
        )]
      )
    }
  }

  TallDoor: {
    -img: "blocks/wall-stone.png"
    -img-closed: [Rendered:image -img {x:64 y:96 w:16 h:48 ay:32}]
    -img-open:   [Rendered:image -img {x:64 y:144 w:16 h:48 ay:32}]

    make: (
      Door:make -img-closed -img-open
    )
  }

  WoodDoor: {
    -img: "blocks/door-wood.png"
    -img-closed: [Rendered:image -img {}]
    -img-open: [Rendered:image -img {
      x: 20 y: 0
      w: 8 h: 23
    }]

    make: (
      Door:make -img-closed -img-open
    )
  }
}]
