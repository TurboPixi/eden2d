[def {
  Door: {
    make: (closed-img open-img | Entity:make [list
      [Located:make]
      [Rendered:make closed-img]
      [Solid:make 1]
      {door: {
        ^: Door:impl
        open-img closed-img
        open: false
        code: 123
      }}
    ])

    impl: {
      prepare: (ent action | if [= action:action :open] (
        if [!= @:code [?= action :opener :player (prev :selected-item) :key :code]]
          (set action {cancel: true})
      ))

      perform: (ent action |
        if [and [= action:action :open] [! action:cancel]] (do
          [set @ {open: [! @:open]}]
          [@:-update ent]
        )
      )

      -update: (ent | do
        [set ent:rendered {image: [if @:open @:open-img @:closed-img]}]
        [if [? ent :solid] (
          set ent:solid {solid: [! ent:solid:solid]}
        )]
      )
    }
  }

  WoodDoor: {
    -img: "blocks/door-wood.png"
    -img-closed: [Rendered:image -img {}]
    -img-open: [Rendered:image -img {
      x: 20 y: 0
      w: 8 h: 23
    }]

    make: (
      Door:make -img-closed -img-open
    )
  }
}]
