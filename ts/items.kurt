[def {
  Brazier = {
    -img = [Render:image "images/tf/tiles/animated/torch.png" {
      x = 19 y = 2
      w = 11 h = 18
      ay = 4
    }]

    make = [|
      {ent = [make-ent]}
      [do
        [Loc:make ent]
        [Render:make ent Brazier:-img]
        ent
      ]
    ]
  }

  Key = {
    -img = [Render:image "images/tf/icons/fullcolor/icons_full_16.png" {
      x = [* 5 16]
      y = [* 22 16]
    }]

    make = [code |
      {ent = [make-ent]}
      [do
        [Loc:make ent]
        [Render:make ent Key:-img]
        [Portable:make ent]
        [KeyComp:make ent code]
        ent
      ]
    ]
  }

  Crate = {
    -img = [Render:image "images/crate.png" {x = 0 y = 0}]

    make = [|
      {ent = [make-ent]}
      [do
        [Loc:make ent]
        [Render:make ent Crate:-img]
        [Portable:make ent]
        ent
      ]
    ]
  }

  Wand = {
    -img = [Render:image "images/tf/icons/fullcolor/icons_full_16.png" {
      x = [* 3 16]
      y = [* 30 16]
    }]

    make = [|
      {ent = [make-ent]}
      [do
        [Loc:make ent]
        [Render:make ent Wand:-img]
        [Portable:make ent]
        [Usable:make ent [user | ent:programmed:block user]]
        [Programmed:make ent]
        [Container:make ent ent:programmed:program]
        ent
      ]
    ]
  }

  -- Entities with the Portable component can be moved.
  Portable = {
    make = [ent |
      def ent {portable = true}
    ]
  }

  Usable = {
    make = [ent fn |
      def ent {usable = {
        ^ = Usable:impl
        fn
      }}
    ]

    impl = {
      perform = [ent action |
        if [= action:action :use]
           [| @:fn action:user]
      ]
    }
  }

  KeyComp = {
    make = [ent code |
      def ent {key = {code}}
    ]
  }

  Container = {
    make = [ent chunk |
      def ent {container = {chunk}}
    ]
  }

  Programmed = {
    make = [ent |
      def ent {programmed = {
        ^ = Programmed:impl
        program = [make-chunk]
      }}
    ]

    impl = {
      block = [user |
        -- Fake placeholder code. Should come from program chunk.
        [| [user:chunk:add [Crate:make]]:move-to user:loc:x user:loc:y]
      ]
    }
  }
}]
