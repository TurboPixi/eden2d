[def {
  WorldPanel = {
    make = [|
      {
        ^ = WorldPanel:impl
        player = nil
        chunk = nil
      }
    ]

    impl = {
      move = [dx dy | @:chunk:perform [action-move @:player dx dy]]

      make-items = [chunk0 chunk1 | do
        [Stairs:make chunk0 1 5 chunk1 0 5 false]
        [Stairs:make chunk1 1 5 chunk0 2 5 true]
        [chunk0:add [Wand:make]]
        [[chunk0:add [Key:make 123]]:move-to 1 0]
        [[chunk0:add [Key:make 456]]:move-to 2 0]
      ]

      enter = [|
        {
          loc = @:player:loc
          portal = [@:player:chunk:top-with loc:x loc:y :portal]
        }
        [if ?:portal [|
          @:chunk:perform [action-enter portal @:player]
        ]]
      ]

      open = [|
        {door = [@:player:top-with :door]}
        [if [!= door nil] [|
          @:chunk:perform [action-open @:player door]
        ]]
      ]

      select-inv = [slot |
        @:player:player:select-inv slot
      ]

      use-selected = [|
        {selected = [@:player:player:selected-item]}
        [if ?:selected [|
          [@:chunk:perform [action-use @:player selected]]
        ]]
      ]

      selected-container = [|
        {target = [@:player:player:inv-target :container]}
        [if ?:target [|
          target:container:chunk
        ]]
      ]

      take = [|
        {target = [@:player:top-with :portable]}
        [if ?:target [|
          {player = @:player:player}
          [[target:jump player:contents]:move-to player:slot 0]
        ]]
      ]

      put = [|
        {
          loc = @:player:loc
          target = [@:player:player:inv-target :portable]
        }
        [if ?:target [|
          [target:jump @:player:chunk]:move-to loc:x loc:y
        ]]
      ]
    }
  }
}]
