[def {
  -- Player component.
  PlayerComp = {
    -img = [RenderComp:image "images/tf/characters/chara3.png" {
      x = 4 y = 7
      w = 17 h = 32
      ax = 0 ay = 16
    }]

    -- Quick utility to make a player entity.
    make-player = [chunk |
      {ent = [make-ent]}
      [do
        [LocComp:make ent]
        [SolidComp:make ent 1]
        [RenderComp:make ent PlayerComp:-img]
        [PlayerComp:make ent]
        [chunk:add ent]
        ent
      ]
    ]

    make = [ent |
      {inv-chunk = [make-chunk]}
      [do
        [set ent {player = {
          ^ = PlayerComp:impl
          slot = 0
          contents = inv-chunk
          cursor-ent = [UI:Cursor:make inv-chunk]
        }}]
        ent
      ]
    ]

    impl = {
      selected-item = [|
        @:contents:top-with @:slot 0 :portable
      ]

      select-inv = [slot | do
        [set @ {slot}]
        [@:cursor-ent:move-to slot 0]
      ]

      inv-target = [comp |
        @:contents:top-with @:slot 0 comp
      ]
    }
  }
}]
